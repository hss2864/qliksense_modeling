Order:
LOAD 고객ID, 날짜, 구매번호, 수량, 금액,
    AutoNumberHash128(고객ID, 날짜) as %Key_link
FROM [lib://Local/모델링편/example_Link Table.xlsx]
(ooxml, embedded labels, table is Order);

Delivery:
LOAD 고객ID, 날짜, 배송ID, 송장번호, 송장수,
    AutoNumberHash128(고객ID, 날짜) as %Key_link
FROM [lib://Local/모델링편/example_Link Table.xlsx]
(ooxml, embedded labels, table is Delivery);

Customer:
LOAD 고객ID, 이메일, 고객명, 성별, 연락처
FROM [lib://Local/모델링편/example_Link Table.xlsx]
(ooxml, embedded labels, table is Customer);

Calendar:
LOAD 날짜, 연도, 월, 일
FROM [lib://Local/모델링편/example_Link Table.xlsx]
(ooxml, embedded labels, table is Calendar);

// LinkTable
LinkTable:
Load Distinct %Key_link, 고객ID, 날짜 Resident Order;

Concatenate (LinkTable) 
Load Distinct %Key_link, 고객ID, 날짜 Resident Delivery;

Concatenate (LinkTable)
Load Distinct 고객ID Resident Customer;

Concatenate (LinkTable)
Load Distinct 날짜 Resident Calendar;

Drop Fields 고객ID, 날짜 From Order, Delivery;
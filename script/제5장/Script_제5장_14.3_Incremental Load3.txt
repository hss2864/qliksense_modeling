// 1. 초기적재한 QVD 데이터에서의 가장 마지막 "수정 날짜" 추출
Modified_Date:
Load Max(Modified_date) as Max_date
FROM [lib://Local/모델링편/example_Incremental Load_Initial.qvd](qvd);

Let vLast_date = Peek('Max_date', 0, 'Modified_Date');
Drop Table Modified_Date;

// 2. Incremental Load (Insert & Update & Delete): "수정 날짜" 기준으로 값 추가/업데이트/삭제
Case_three:
LOAD
    Region,
    ID,
    Product,
    Value,
    Modified_date
FROM [lib://Local/모델링편/example_Incremental Load.xlsx]
(ooxml, embedded labels, table is case3)
Where Modified_date > $(vLast_date);

// 3. 이전 데이터에서 중복되는 Key(ID)는 제외 후 붙이기
Concatenate (Case_three)
LOAD
    Region,
    ID,
    Product,
    Value,
    Modified_date
FROM [lib://Local/모델링편/example_Incremental Load_Initial.qvd](qvd)
Where Not Exists(ID);

// 4. Key(ID)를 기준으로 삭제된 데이터는 제외
Inner Join (Case_three) 
LOAD ID 
FROM [lib://Local/모델링편/example_Incremental Load.xlsx]
(ooxml, embedded labels, table is case3);

// 5. 증분적재 (QVD 저장)
Store Case_three into 'lib://Local/모델링편/Incremental Load_case3.qvd' (qvd) ;
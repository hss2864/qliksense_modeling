Register:
LOAD
    ID,
    이름,
    성별,
    생년월일,
    유형,
    가입여부,
    가입일자
FROM [lib://Local/모델링편/example_For Next.xlsx]
(ooxml, embedded labels, table is Register);

// Step 1. "가입일자" 기준, 마스터 캘린더 생성
Calendar:
Load Distinct
	  date(monthstart(tmpdate), 'YYYY. M.') as 기준년월,
	  monthstart(tmpdate) as 기준일;
Load date(mindate + IterNo()) as tmpdate
While mindate + IterNo() <= maxdate;
Load min(FieldValue('가입일자', recno()))-1 as mindate,
	  max(FieldValue('가입일자', recno()))+0 as maxdate
AutoGenerate FieldValueCount('가입일자');

// Step 2. 반복 실행할 횟수를 "변수"에 저장
Let vCnt = NoOfRows('Calendar');
Trace -- 반복 실행 총 횟수 : $(vCnt);

// Step 3. 월별 "가입누적년수" 계산
For i = 0 to $(vCnt) -1  // Loop 시작

  Let v기준년월 = Peek('기준년월', $(i), 'Calendar'); // Snapshot 기준
  Let v기준일 = Peek('기준일', $(i), 'Calendar');	    // 계산 시 사용

  Trace -- 기준년월 : $(v기준년월);

  Snapshot:
  Load ID,
       '$(v기준년월)' as 기준년월,
       Num(Ceil(('$(v기준일)'-monthstart(가입일자))/365, 0.1), '0.0') as 가입누적년수
  Resident Register
  Where date(monthstart(가입일자)) <= '$(v기준일)';

Next i  // Loop 종료

Drop Table Calendar; // 불필요 테이블 삭제